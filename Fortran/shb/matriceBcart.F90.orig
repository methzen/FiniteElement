! --------------------------------------------------------------------
! Copyright (C) 1991 - 2020 - EDF R&D - www.code-aster.org
! This file is part of code_aster.
!
! code_aster is free software: you can redistribute it and/or modify
! it under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version.
!
! code_aster is distributed in the hope that it will be useful,
! but WITHOUT ANY WARRANTY without even the implied warranty of
! MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
! GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License
! along with code_aster.  If not, see <http://www.gnu.org/licenses/>.
! --------------------------------------------------------------------
!
subroutine matriceBcart(nb_node  , geom_init,&
                        nb_dof   , disp     ,&
                        B0       , BZETA    , BZETAZETA,&
                        BXI_     , BETA_    , &
                        BXIZETA_ , BETAZETA_, BXIETA_,&
                        det_)
!
implicit none
!
#include "asterf_types.h"
#include "asterfort/matriceTdecomp.h"
#include "asterfort/matriceT.h"
#include "asterfort/matriceBc.h"
#include "asterfort/Jacobien.h"
#include "asterfort/matinv.h"
#include "asterfort/assert.h"
!
integer, intent(in) :: nb_node, nb_dof
real(kind=8), intent(in)  :: geom_init(3*nb_node), disp(nb_dof)
real(kind=8), intent(out) :: B0(6,3*nb_node), BZETA(6,3*nb_node), BZETAZETA(6,3*nb_node)
real(kind=8), optional, intent(out) :: BXI_(6,3*nb_node), BETA_(6,3*nb_node),BXIETA_(6,3*nb_node)
real(kind=8), optional, intent(out) :: BXIZETA_(6,3*nb_node), BETAZETA_(6,3*nb_node)
real(kind=8), optional, intent(out) :: det_
!
! --------------------------------------------------------------------------------------------------
!
! Solid-shell element
!
! Compute the gradient matrix in cartesian frame
!
! --------------------------------------------------------------------------------------------------
!
! In  nb_node          : number of nodes of element without pinch node(s)
! In  geom_init        : initial coordinates of element
! In  nb_dof           : number of dof
! In  disp             : displacements of element
! Out B0               : constant part of gradient matrix
! Out BXI              : xi part of gradient matrix
! Out BETA             : eta part of gradient matrix
! Out BZETA            : zeta part of gradient matrix
! Out BXIZETA          : xi x zeta part of gradient matrix
! Out BETAZETA         : eta x zeta part of gradient matrix
! Out BZETAZETA        : zeta x zeta part of gradient matrix
! Out det              : determinant of initial jacobian matrix
!
! --------------------------------------------------------------------------------------------------
!
    real(kind=8) :: Bc0(6,24), BcZETA(6,24), BcZETAZETA(6,24), BcXI(6,24), geom_curr(24)
    real(kind=8) :: BcETA(6,24), BcETAZETA(6,24), BcXIZETA(6,24),BcXIETA(6,24)
    real(kind=8) :: TXI(6,6), TETA(6,6), TZETA(6,6), T0(6,6), J0(3,3), Jm1(3,3)
    real(kind=8) :: det
!
! --------------------------------------------------------------------------------------------------
!
    ASSERT(nb_node .eq. 8)
    ASSERT(nb_dof .eq. 25)
!
    geom_curr(:) = geom_init(:) + disp(1:3*nb_node)
!
! - Compute initial jacobian matrix at center of element
!
    call Jacobien(nb_node, geom_init, (/0.d0, 0.d0, 0.d0/), J0)
!
! - Inverse jacobian matrix
!
    call matinv('S', 3, J0, Jm1, det)
    !WRITE(6,*) 'Jacobien: ',J0,Jm1,det
!
! - Compute gradient matrix in covariant basis
!
    call matriceBc(nb_node , geom_curr, Bc0       ,&
                   BcXI    , BcETA    , BcZETA    ,&
                   BcXIZETA, BcETAZETA, BcZETAZETA,BcXIETA)
!
! - Compute the decomposition of the inverse Jacobian matrix
!
    call matriceTdecomp(nb_node, geom_init, TXI, TETA, TZETA)
    !print*,'TXI',TXI
    !print*,'TETA',TETA
    !print*,'TZETA',TZETA
!
! - Compute T matrix (matrix relating the covariant and cartesian frames) at center of element
!
    call matriceT(nb_node, geom_init, (/0.d0,0.d0,0.d0/), T0)
    !WRITE(6,*) 'DÃ©composition jacobien: ',TXI, TETA, TZETA, T0
!
! - Decomposition of gradient matrix
!
    B0        = matmul(T0,Bc0)
    BZETA     = matmul(T0,BcZETA)+matmul(TZETA,Bc0)
    BZETAZETA = matmul(T0,BcZETAZETA)+matmul(TZETA,BcZETA)
    if (present(BXI_)) then
        BXI_      = matmul(T0,BcXI)+matmul(TXI,Bc0)
        BETA_     = matmul(T0,BcETA)+matmul(TETA,Bc0)
        BETAZETA_ = matmul(T0,BcETAZETA)+matmul(TETA,BcZETA)+matmul(TZETA,BcETA)
        BXIZETA_  = matmul(T0,BcXIZETA)+matmul(TXI,BcZETA)+matmul(TZETA,BcXI)
        BXIETA_   = (matmul(T0,BcXIETA)+matmul(TXI,BcETA)+matmul(TETA,BcXI))
    endif
!
    if (present(det_)) then
        det_ = det
    endif
!
end subroutine
